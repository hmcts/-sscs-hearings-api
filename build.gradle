plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.12.RELEASE'
  id 'org.springframework.boot' version '2.7.18'
  id 'uk.gov.hmcts.java' version '0.12.27'
  id 'org.owasp.dependencycheck' version '10.0.3'
  id 'com.github.ben-manes.versions' version '0.42.0'
  id 'org.sonarqube' version '4.3.0.3225'
  id 'info.solidsoft.pitest' version '1.9.11'
  id 'com.gorylenko.gradle-git-properties' version '2.4.1'
  id 'au.com.dius.pact' version '4.3.10'
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'info.solidsoft.pitest'

gitProperties {
 gitPropertiesDir = new File("${project.rootDir}/src/main/resources/uk/gov/hmcts/reform/sscs")
  keys = ['git.commit.id', 'git.commit.time']
  dateFormat = "yyyy-MM-dd'T'HH:mmZ"
  dateFormatTimeZone = "GMT"
}

generateGitProperties.dependsOn(':installLocalGitHook')
processResources.dependsOn(':generateGitProperties')

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }

  contractTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/contractTest/java')
    }
    resources.srcDir file('src/contractTest/resources')
  }
}

tasks.register('integration', Test) {
  description = "Runs integration tests"
  group = "Verification"
  setTestClassesDirs(sourceSets.integrationTest.output.classesDirs)
  setClasspath(sourceSets.integrationTest.runtimeClasspath)
}

tasks.register('functional', Test) {
  group = 'Verification'
  description = 'Executes non-destructive functional tests in AAT against a running CCD and notification service.'
  setTestClassesDirs(sourceSets.functionalTest.output.classesDirs)
  setClasspath(sourceSets.functionalTest.runtimeClasspath)
}

tasks.register('smoke', Test) {
  group = 'Verification'
  description = 'Executes non-destructive smoke tests against a running SSCS hearings API application.'
  setTestClassesDirs(sourceSets.smokeTest.output.classesDirs)
  setClasspath(sourceSets.smokeTest.runtimeClasspath)
}

tasks.register('contract', Test) {
  group = 'Verification'
  description = 'Executes consumer Pact tests.'
  setTestClassesDirs(sourceSets.contractTest.output.classesDirs)
  setClasspath(sourceSets.contractTest.runtimeClasspath)
}

tasks.register('runAndPublishConsumerPactTests', Test) {
  logger.lifecycle("Runs pact Tests")
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
}

runAndPublishConsumerPactTests.dependsOn contract
runAndPublishConsumerPactTests.finalizedBy pactPublish

configurations {
  functionalTestImplementation.extendsFrom testImplementation
  functionalTestRuntimeOnly.extendsFrom testRuntimeOnly

  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom testRuntimeOnly

  smokeTestImplementation.extendsFrom testImplementation
  smokeTestRuntimeOnly.extendsFrom testRuntimeOnly

  contractTestImplementation.extendsFrom testImplementation
  contractTestRuntimeOnly.extendsFrom testRuntimeOnly
}

tasks.withType(JavaCompile).configureEach {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror" << '-Xlint:deprecation'
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

checkstyle {
  maxWarnings = 0
  toolVersion = '10.12.1'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "6.55.0"
  reportsDir = layout.buildDirectory.dir("reports/pmd").get().asFile
  // https://github.com/pmd/pmd/issues/876
  ruleSets = []
  ruleSetFiles = files("config/pmd/ruleset.xml")
  group = 'Verification'
  consoleOutput = false
}

pmdMain {
  group = 'Verification'
  ignoreFailures = false
}

pmdTest {
  group = 'Verification'
  ignoreFailures = true
}

pmdContractTest {
  group = 'Verification'
  ignoreFailures = true
}

pmdSmokeTest {
  group = 'Verification'
  ignoreFailures = true
}

pmdFunctionalTest {
  group = 'Verification'
  ignoreFailures = true
}

pmdIntegrationTest {
  group = 'Verification'
  ignoreFailures = true
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required = true
    csv.required = false
    xml.outputLocation = layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml")
  }
}

project.tasks.named('sonarqube') {
  dependsOn test, integration, jacocoTestReport
}

check.dependsOn integration
checkstyleMain.shouldRunAfter(compileJava)
test.shouldRunAfter(checkstyleTest)
integration.shouldRunAfter(checkstyleIntegrationTest)
integration.shouldRunAfter(test)

sonarqube {
  properties {
    property "sonar.projectName", "SSCS - Hearings Api"
    property "sonar.projectKey", "SSCSHA"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.outputLocation}"
    property "sonar.pitest.mode", "reuseReport"
    property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/sscs/HearingsApiApplication.java," +
            "src/main/java/uk/gov/hmcts/reform/sscs/config/**," +
            "src/main/java/uk/gov/hmcts/reform/sscs/model/**," +
            "src/main/java/uk/gov/hmcts/reform/sscs/service/holder/**," +
            "src/main/java/uk/gov/hmcts/reform/sscs/exception/**,"
  }
}

pitest {
  junit5PluginVersion = '0.15'
  targetClasses = ['uk.gov.hmcts.reform.sscs.*']
  excludedClasses = [
    'uk.gov.hmcts.reform.sscs.config.*',
    'uk.gov.hmcts.reform.sscs.HearingsApiApplication'
  ]
  threads = 10
  outputFormats = ['XML', 'HTML']
  timestampedReports = false
  mutationThreshold = 80
}

tasks.register('installLocalGitHook', Copy) {
  from new File(rootProject.rootDir, 'config/git/pre-commit')
  into { new File(rootProject.rootDir, '.git/hooks') }
  fileMode 0775
}

application {
  mainClass.set('uk.gov.hmcts.reform.sscs.HearingsApiApplication')
  compileJava.dependsOn installLocalGitHook
}


tasks.register('runGitPreCommitTasks') {
  dependsOn 'test'
  dependsOn 'pmdMain'
  dependsOn 'pmdTest'
  dependsOn 'checkstyleMain'
  dependsOn 'checkstyleTest'
}

project.tasks.named('pitest') {
  group = "Verification"
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 7-10 fails the build, anything lower and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 7.0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'

  nvd.validForHours = 24

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
    retirejs {
      enabled = false
    }
    ossIndex {
      enabled = false
    }
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://jitpack.io' }
  // Requires authentication, if it gets here the library cannot be found!
  maven {
    url 'https://repo.spring.io/libs-milestone'
  }
}

def versions = [
  springsecurity : '5.7.12',
  tomcat         : '9.0.86',
  lombok         : '1.18.28',
  jackson        : '2.15.2'
]

dependencies {
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter', version: springBoot.class.package.implementationVersion
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBoot.class.package.implementationVersion
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: springBoot.class.package.implementationVersion
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: springBoot.class.package.implementationVersion
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json', version: springBoot.class.package.implementationVersion
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation', version: springBoot.class.package.implementationVersion
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: springBoot.class.package.implementationVersion
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-activemq', version: springBoot.class.package.implementationVersion

  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-resource-server', version: versions.springsecurity
  implementation group: 'org.springframework.security', name: 'spring-security-crypto', version: versions.springsecurity
  implementation group: 'org.springframework.security', name: 'spring-security-web', version: versions.springsecurity
  implementation group: 'org.springframework.security', name: 'spring-security-config', version: versions.springsecurity
  implementation group: 'org.springframework.security', name: 'spring-security-core', version: versions.springsecurity
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-core', version: versions.springsecurity

  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter'

  implementation group: 'org.springframework.retry', name: 'spring-retry', version: '2.0.3'

  implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: versions.tomcat
  implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-el', version: versions.tomcat
  implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-websocket', version: versions.tomcat

  implementation group: 'org.apache.qpid', name: 'qpid-jms-client', version: '1.8.0'

  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: '2.17.1'

  implementation group: 'commons-io', name: 'commons-io', version: '2.13.0'

  implementation group: 'io.github.openfeign', name: 'feign-httpclient', version: '11.10'
  implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.12.0'

  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.6.15'

  implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '7.4'
  implementation group: 'com.github.hmcts', name: 'java-logging', version: '6.0.1'

  implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '4.0.3'
  implementation group: 'com.github.hmcts', name: 'ccd-client', version: '4.9.2'

  implementation group: 'org.projectlombok', name:'lombok', version: versions.lombok
  annotationProcessor group: 'org.projectlombok', name:'lombok', version: versions.lombok

  implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.13'
  implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.13'

  implementation group: 'com.github.hmcts', name: 'sscs-common', version: '7e2a08f'
  implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.36'

  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jackson
  implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: versions.jackson
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson

  implementation group: 'com.networknt', name: 'json-schema-validator', version: '1.0.86'

  implementation group: 'com.azure', name: 'azure-core', version: '1.37.0'
  implementation group: 'com.azure', name: 'azure-messaging-servicebus', version: '7.14.0-beta.1'

  implementation group: 'org.elasticsearch', name: 'elasticsearch', version: '7.17.12'

  implementation group: 'com.microsoft.azure', name: 'applicationinsights-core', version: '2.6.4'
  // https://mvnrepository.com/artifact/com.microsoft.azure/applicationinsights-web
  implementation group: 'com.microsoft.azure', name: 'applicationinsights-web', version: '2.6.4'

  testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '4.5.1'
  testImplementation group: 'io.rest-assured', name: 'xml-path', version: '4.5.1'
  testImplementation group: 'io.rest-assured', name: 'json-path', version: '4.5.1'

  testImplementation group: 'org.projectlombok', name:'lombok', version: versions.lombok
  testAnnotationProcessor group: 'org.projectlombok', name:'lombok', version: versions.lombok

  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.8.2'
  testRuntimeOnly  group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.8.2'

  testImplementation group: 'org.pitest', name: 'pitest', version: '1.14.2'
  testImplementation group: 'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin', version: pitest.pitestVersion.get()
  testImplementation group: 'org.codehaus.sonar-plugins', name: 'sonar-pitest-plugin', version: '0.5'

  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.22.0'
  testImplementation group: 'commons-io', name: 'commons-io', version: '2.13.0'


  testImplementation group: 'com.github.tomakehurst', name: 'wiremock', version: '2.27.2'
  testImplementation group: 'org.mockito', name: 'mockito-inline', version: '5.1.1'

  //pact contract testing
  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: '4.1.7'
  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'java8', version: '4.1.7'
  contractTestRuntimeOnly group: 'au.com.dius.pact.consumer', name: 'junit5', version: '4.1.7'
  contractTestRuntimeOnly group: 'au.com.dius.pact.consumer', name: 'java8',  version: '4.1.7'
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:2021.0.1"
  }
  dependencies {
    // CVE-2022-38752
    dependency group: 'org.yaml', name: 'snakeyaml', version: '2.0'

    // CVE-2023-33202
    dependency group: 'org.springframework.security', name: 'spring-security-rsa', version: '1.1.1'
  }
}

bootJar {
  archiveFileName = "sscs-hearings-api.jar"

  manifest {
    attributes('Implementation-Title': project.name, 'Implementation-Version': project.version.toString())
  }
}

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
rootProject.tasks.named("processSmokeTestResources") {
  duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

rootProject.tasks.named("processFunctionalTestResources") {
  duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

rootProject.tasks.named("processContractTestResources") {
  duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

rootProject.tasks.named("processIntegrationTestResources") {
  duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

tasks.register('fortifyScan', JavaExec) {
  mainClass = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

project.ext {
  pacticipant = 'sscs-hearings-api'
  pacticipantVersion = getCheckedOutGitCommitHash()
}

pact {
  broker {
    pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
  }
  publish {
    pactDirectory = 'pacts'
    tags = [System.getenv("PACT_BRANCH_NAME") ?:'Dev']
    version = project.pacticipantVersion
  }
}

static def getCheckedOutGitCommitHash() {
  'git rev-parse --verify --short HEAD'.execute().text.trim()
}

run {
  def debug = System.getProperty("debug")
  if (debug == 'true') {
    jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5006']
  }
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}
