plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'org.springframework.boot' version '2.7.0'
  id 'uk.gov.hmcts.java' version '0.12.18'
  id 'org.owasp.dependencycheck' version '7.1.0.1'
  id 'com.github.ben-manes.versions' version '0.42.0'
  id 'org.sonarqube' version '3.3'
  id 'info.solidsoft.pitest' version '1.7.0'
  id 'com.gorylenko.gradle-git-properties' version '2.4.0'
  id 'au.com.dius.pact' version '4.3.5'
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'info.solidsoft.pitest'

gitProperties {
 gitPropertiesDir = new File("${project.rootDir}/src/main/resources/uk/gov/hmcts/reform/sscs")
  keys = ['git.commit.id', 'git.commit.time']
  dateFormat = "yyyy-MM-dd'T'HH:mmZ"
  dateFormatTimeZone = "GMT"
}

processResources.dependsOn(':generateGitProperties')

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(11)
  }
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }

  contractTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/contractTest/java')
    }
    resources.srcDir file('src/contractTest/resources')
  }
}

task integration(type: Test) {
  description = "Runs integration tests"
  group = "Verification"
  setTestClassesDirs(sourceSets.integrationTest.output.classesDirs)
  setClasspath(sourceSets.integrationTest.runtimeClasspath)
}

task functional(type: Test) {
  group = 'Verification'
  description = 'Executes non-destructive functional tests in AAT against a running CCD and notification service.'
  setTestClassesDirs(sourceSets.functionalTest.output.classesDirs)
  setClasspath(sourceSets.functionalTest.runtimeClasspath)
}

task smoke(type: Test) {
  group = 'Verification'
  description = 'Executes non-destructive smoke tests against a running SSCS hearings API application.'
  setTestClassesDirs(sourceSets.smokeTest.output.classesDirs)
  setClasspath(sourceSets.smokeTest.runtimeClasspath)
}

task contract(type: Test) {
  group = 'Verification'
  description = 'Executes consumer Pact tests.'
  setTestClassesDirs(sourceSets.contractTest.output.classesDirs)
  setClasspath(sourceSets.contractTest.runtimeClasspath)
}

task runAndPublishConsumerPactTests(type: Test){
  logger.lifecycle("Runs pact Tests")
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
}

runAndPublishConsumerPactTests.dependsOn contract
runAndPublishConsumerPactTests.finalizedBy pactPublish

configurations {
  functionalTestImplementation.extendsFrom testImplementation
  functionalTestRuntimeOnly.extendsFrom testRuntimeOnly

  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom testRuntimeOnly

  smokeTestImplementation.extendsFrom testImplementation
  smokeTestRuntimeOnly.extendsFrom testRuntimeOnly

  contractTestImplementation.extendsFrom testImplementation
  contractTestRuntimeOnly.extendsFrom testRuntimeOnly
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror" << '-Xlint:deprecation'
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

checkstyle {
  maxWarnings = 0
  toolVersion = '9.2.1'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "6.42.0"
  reportsDir = file("$project.buildDir/reports/pmd")
  // https://github.com/pmd/pmd/issues/876
  ruleSets = []
  ruleSetFiles = files("config/pmd/ruleset.xml")
  group = 'Verification'
}

pmdMain {
  group = 'Verification'
  ignoreFailures = false
}

pmdTest {
  group = 'Verification'
  ignoreFailures = true
}

pmdContractTest {
  group = 'Verification'
  ignoreFailures = true
}

pmdSmokeTest {
  group = 'Verification'
  ignoreFailures = true
}

pmdFunctionalTest {
  group = 'Verification'
  ignoreFailures = true
}

pmdIntegrationTest {
  group = 'Verification'
  ignoreFailures = true
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required = true
    csv.required = false
    xml.destination file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport
check.dependsOn integration
checkstyleMain.shouldRunAfter(compileJava)
test.shouldRunAfter(checkstyleTest)
integration.shouldRunAfter(checkstyleIntegrationTest)
integration.shouldRunAfter(test)

sonarqube {
  properties {
    property "sonar.projectName", "SSCS - Hearings Api"
    property "sonar.projectKey", "SSCSHA"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
    property "sonar.pitest.mode", "reuseReport"
    property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/sscs/HearingsApiApplication.java," +
            "src/main/java/uk/gov/hmcts/reform/sscs/config/**," +
            "src/main/java/uk/gov/hmcts/reform/sscs/model/**,"
  }
}

pitest {
  junit5PluginVersion = '0.15'
  targetClasses = ['uk.gov.hmcts.reform.sscs.*']
  excludedClasses = [
    'uk.gov.hmcts.reform.sscs.config.*',
    'uk.gov.hmcts.reform.sscs.HearingsApiApplication'
  ]
  threads = 10
  outputFormats = ['XML', 'HTML']
  timestampedReports = false
  mutationThreshold = 80
}

project.tasks['pitest'].group = "Verification"
// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 7-10 fails the build, anything lower and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 7.0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://jitpack.io' }
  // Requires authentication, if it gets here the library cannot be found!
  maven {
    url 'https://repo.spring.io/libs-milestone'
  }
}

def versions= [
  assertJ: '3.22.0',
  azureCore: '1.26.0',
  azureMessagingServicebus: '7.7.0',
  commonsIo: '2.11.0',
  fortify: '1.2.2',
  feign: '11.7',
  gradlePiTestPlugin: '1.5.2',
  hmctsProperties: '0.1.0',
  jackson: '2.13.2',
  jacksonBom: '2.13.2.20220324',
  jacksonDatabind: '2.13.2.1',
  jacksonDatatype: '2.13.2',
  junitJupiter: '5.8.2',
  jsonSchemaValidator: '1.0.66',
  log4J: '2.17.1',
  logback: '1.2.10',
  lombok: '1.18.22',
  okhttp: '4.9.2',
  pact: '4.1.7',
  pactJava8: '4.1.34',
  pactJunit: '4.3.5',
  pitest: '1.7.3',
  reformLogging: '5.1.9',
  restAssured: '4.5.1',
  serviceAuthProviderJavaClient: '4.0.3',
  slf4j: '1.7.36',
  sonarPiTestPlugin: '0.5',
  springCloud: '2021.0.1',
  springDoc: '1.6.6',
  springRetry: '1.3.3',
  sscsCommon: '4.9.4',
  tomcat: '9.0.63',
  wiremock: '2.27.2'
]

dependencies {
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'

  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-resource-server'

  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter'

  implementation group: 'org.springframework.retry', name: 'spring-retry', version: versions.springRetry

  implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: versions.tomcat
  implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-el', version: versions.tomcat
  implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-websocket', version: versions.tomcat

  implementation group: 'commons-io', name: 'commons-io', version: versions.commonsIo

  implementation group: 'io.github.openfeign', name: 'feign-httpclient', version: versions.feign
  implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: versions.okhttp

  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: versions.springDoc

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: versions.reformLogging
  implementation group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: versions.reformLogging

  implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client',
          version: versions.serviceAuthProviderJavaClient

  implementation group: 'org.projectlombok', name:'lombok', version:versions.lombok
  annotationProcessor group: 'org.projectlombok', name:'lombok', version:versions.lombok

  implementation group: 'ch.qos.logback', name: 'logback-core', version: versions.logback
  implementation group: 'ch.qos.logback', name: 'logback-classic', version: versions.logback

  implementation group: 'com.github.hmcts', name: 'properties-volume-spring-boot-starter', version: versions.hmctsProperties
  implementation group: 'com.github.hmcts', name: 'sscs-common', version: versions.sscsCommon

  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: versions.log4J

  implementation group: 'org.slf4j', name: 'slf4j-api', version: versions.slf4j

  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson
  implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: versions.jacksonDatatype

  implementation group: 'com.networknt', name: 'json-schema-validator', version: versions.jsonSchemaValidator

  implementation group: 'com.azure', name: 'azure-core', version: versions.azureCore
  implementation group: 'com.azure', name: 'azure-messaging-servicebus', version: versions.azureMessagingServicebus

  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jacksonDatabind
  implementation group: 'com.fasterxml.jackson', name: 'jackson-bom', version: versions.jacksonBom

  testImplementation group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
  testImplementation group: 'io.rest-assured', name: 'xml-path', version: versions.restAssured
  testImplementation group: 'io.rest-assured', name: 'json-path', version: versions.restAssured

  testImplementation group: 'org.projectlombok', name:'lombok', version:versions.lombok
  testAnnotationProcessor group: 'org.projectlombok', name:'lombok', version:versions.lombok

  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junitJupiter
  testRuntimeOnly  group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junitJupiter

  testImplementation group: 'org.pitest', name: 'pitest', version: versions.pitest
  testImplementation group: 'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin',
          version: versions.gradlePiTestPlugin
  testImplementation group: 'org.codehaus.sonar-plugins', name: 'sonar-pitest-plugin',
          version: versions.sonarPiTestPlugin

  testImplementation group: 'org.assertj', name: 'assertj-core', version: versions.assertJ
  testImplementation group: 'commons-io', name: 'commons-io', version: versions.commonsIo

  testImplementation group: 'com.github.tomakehurst', name: 'wiremock', version: versions.wiremock

  //pact contract testing
  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact
  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'java8', version: versions.pact
  contractTestRuntimeOnly group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact
  contractTestRuntimeOnly group: 'au.com.dius.pact.consumer', name: 'java8',  version: versions.pact
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${versions.springCloud}"
  }
  dependencies {
    //CVE-2016-1000027
    dependencySet(group: 'org.springframework', version: '5.3.20') {
      entry 'spring-aop'
      entry 'spring-aspects'
      entry 'spring-beans'
      entry 'spring-context'
      entry 'spring-context-support'
      entry 'spring-core'
      entry 'spring-expression'
      entry 'spring-jcl'
      entry 'spring-jdbc'
      entry 'spring-orm'
      entry 'spring-tx'
      entry 'spring-web'
      entry 'spring-webmvc'
    }
  }
}

mainClassName = 'uk.gov.hmcts.reform.sscs.HearingsApiApplication'

bootJar {
  archiveFileName = "sscs-hearings-api.jar"

  manifest {
    attributes('Implementation-Title': project.name, 'Implementation-Version': project.version.toString())
  }
}

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
rootProject.tasks.named("processSmokeTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processFunctionalTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processContractTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processIntegrationTestResources") {
  duplicatesStrategy = 'include'
}

task fortifyScan(type: JavaExec)  {
  mainClass = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

project.ext {
  pacticipant = 'sscs-hearings-api'
  pacticipantVersion = getCheckedOutGitCommitHash()
}

pact {
  broker {
    pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
  }
  publish {
    pactDirectory = 'pacts'
    tags = [System.getenv("PACT_BRANCH_NAME") ?:'Dev']
    version = project.pacticipantVersion
  }
}

static def getCheckedOutGitCommitHash() {
  'git rev-parse --verify --short HEAD'.execute().text.trim()
}

run {
  def debug = System.getProperty("debug")
  if (debug == 'true') {
    jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5006']
  }
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}
